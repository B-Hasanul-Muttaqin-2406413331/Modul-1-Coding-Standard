plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

ext {
    seleniumJavaVersion = '4.14.1'
    seleniumJupiterVersion = '5.0.1'
    webdrivermanagerVersion = '5.6.3'
}

group = 'id.ac.ui.cs.advprog'
version = '0.0.1-SNAPSHOT'
description = 'eshop'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}


repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-thymeleaf-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation "org.seleniumhq.selenium:selenium-java:${seleniumJavaVersion}"
    testImplementation "io.github.bonigarcia:selenium-jupiter:${seleniumJupiterVersion}"
    testImplementation "io.github.bonigarcia:webdrivermanager:${webdrivermanagerVersion}"
}

tasks.register('unitTest', Test) {
    description = "Runs unit tests."
    group = "verification"

    filter {
        excludeTestsMatching("*FunctionalTest")
    }
}

tasks.register('functionalTest', Test) {
    description = "Runs functional tests."
    group = "verification"

    filter {
        includeTestsMatching("*FunctionalTest")
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.13"
}

tasks.named('test') {
    finalizedBy tasks.named('jacocoTestReport')
}

tasks.named('jacocoTestReport') {
    dependsOn tasks.named('test')
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.named('jacocoTestCoverageVerification') {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('jacocoTestCoverageVerification')
}
